sub namedPoint name$
  box 1e-30 1e-30 name name$
end sub


sub joining obj1$ obj2$
  amove ptx(obj1$.tc) pty(obj2$.rc)
  namedPoint temp
  join obj1$.tc - temp
  join temp -> obj2$.rc
end sub

sub writeMaxWidth toWrite$ maxWidth origHei
  Hei=origHei
  set hei Hei
  if twidth(toWrite$) > maxWidth then
    Hei=Hei*maxWidth/twidth(toWrite$)
  end if
  set hei Hei
  write toWrite$
end sub



sub subbox title$ n label$
  box boxwidth rubrichei name label$+num$(n-1) justify TL round .05
  rmove boxwidth-.15 -rubrichei/2
  set hei .8*rubrichei font tt just cr
  write n-1
  rmove -.3 0
  writeMaxWidth title$ boxwidth-1 .6*rubrichei
  rmove -boxwidth+.45 -rubrichei/2
end sub


sub act actname$ label$ ianame1$ ianame2$ ianame3$ ianame4$
  default ianame3 ""
  default ianame4 ""

  if ianame3$ = "" then
    num=2
  else if ianame4$ = "" then
    num=3
  else 
    num=4
  end if

  set lwidth defaultlw
  box boxwidth titlehei+rubrichei*num justify TL fill snow name label$ round .05
  rmove boxwidth -titlehei
  rline -boxwidth 0

  set lwidth defaultlw/2
  subbox ianame1$ 1 label$
  subbox ianame2$ 2 label$
  if not (ianame3$ = "") then
    subbox ianame3$ 3 label$
  end if
  if not (ianame4$ = "") then
    subbox ianame4$ 4 label$
  end if

  rmove boxwidth/2 num*rubrichei+titlehei/2
  set font ttb just cc
  writeMaxWidth actname$ boxwidth-.5 .8*titlehei

end sub


sub free freename$ numero colorname$
  set color colorname$ hei .5*titlehei font tt
  box freewidth freeheight fill snow just TL name "free"+num$(numero) round .05

  rmove freewidth/2 -freeheight/3
  write "Free Nr."+num$(numero)

  rmove 0 -freeheight/3
  set font ttb
  writeMaxWidth freename$ freewidth-.25 .6*titlehei

end sub
