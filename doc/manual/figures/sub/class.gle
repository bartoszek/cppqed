sub class classname$ parameters$ h additional
  default parameters ""
  default h          1
  default additional 0
  set just tc
  begin box add 0.2 round 0.1 name classname$ fill snow
    set hei h*defaulthei font pspr
    if additional>0 then
      set color snow
      box 0.1 additional justify TC
      set color black 
    end if
    write classname$
    if not (parameters$ = "") then
      rmove 0 -sqrt(h)*.5
      set hei .75*defaulthei font tt
      write parameters$
      set hei defaulthei font pspr
      rmove 0 sqrt(h)*.5
    end if
    set hei defaulthei
  end box
end sub


sub inheritance classname$ text$ origo$
  default origo "tc"
  amove ptx(classname$.origo$)+.05 pty(classname$.origo$)+.05
  set just bl font tt hei .6*defaulthei
  write text$
  rmove -.05 -.05-.6*defaulthei
  set just tc
end sub


begin object selector condition$ scale mul straight
  default scale 1
  default mul 1
  default straight 1
  begin path stroke fill palegreen
    rmove 0 0
    rline 2.5*scale 1*scale*mul
    rline 2.5*scale -1*scale*mul
    closepath
  end path
    rmove 2.5*scale .4*scale*mul
    set just cc hei .5*defaulthei
    write condition$
    rmove -1.75*scale -.3*scale*mul
    set hei .4*defaulthei
    if straight=1 then
      text true
    else
      text false
    end if
    rmove 3.5*scale 0
    if straight=1 then
      text false
    else
      text true
    end if
end object
