size 20 19

defaulthei = 0.36
set lwidth 0.04 hei defaulthei just tc font pspr arrowsize 0.4


sub class classname$ parameters$ h
  default parameters ""
  default h          1
  set just tc
  begin box add 0.2 round 0.1 name classname$ fill snow
    set hei h*defaulthei
    write classname$
    if not (parameters$ = "") then
      rmove 0 -.75
      set hei .75*defaulthei font tt
      write parameters$
      set hei defaulthei font pspr
      rmove 0 .75
    end if
    set hei defaulthei
  end box
end sub


amove 8 18

set lwidth .1 lstyle 2 just tc color gray40 hei 1 font plge

box 12 8 justify tc round 0.1 name "core" fill gray10
      set color black lstyle 0 lwidth 0
      rmove 0 -.25
      text Core

rmove 0 -1.25
class "quantumdata" "StateVector, DensityOperator,\\LazyDensityOperator" 2

rmove 3.5 -2.5
class "structure" "QuantumSystem, Free,\\Interaction, SubSystem,\\Hamiltonian, Liouvillean,\\Exact, Averaged" 2

rmove -6 -2.5
class "quantumtrajectory" "MCWF\_Trajectory, EnsembleMCWF, Master" 2

set lwidth .05

join structure.tc -> quantumdata.br
join quantumtrajectory.tc -> quantumdata.v
join quantumtrajectory.tr -> structure.bl



rmove 4 -4

set lwidth .1 lstyle 2 just tc color gray40 hei 1 font plge

box 10 7 justify tc round 0.1 name "elements" fill gray10
      set color black lstyle 0 lwidth 0
      rmove 0 -.25
      text Elements

rmove 0 -1.25
class "frees" "Mode, Spin, etc." 2

rmove 2.5 -2
class "interactions" "JaynesCummings, etc." 2

rmove -5 -2
class "composites" "BinarySystem, Composite" 2

set lwidth .05

join interactions.tc -> frees.br
join composites.tc -> frees.bl
join composites.tr -> interactions.bl

set lwidth .08 color gray60 arrowsize .8

join elements.tc -> structure.bc

rmove -7.5 -8

set lwidth .1 lstyle 2 just tc color gray40 hei 1 font plge arrowsize 0.4

box 6 3 justify tc round 0.1 name "helpers" fill gray10
      set color black lstyle 0 lwidth 0
      rmove 0 -.25
      text Helpers

rmove 0 -1.25
class "quantumoperator" "Tridiagonal, Frequencies, Sigma" 1.8

set lwidth .05

join quantumoperator.tl -> quantumdata.lc curve 90 0 10 -5

set lstyle 2 lwidth .08 color gray60 arrowsize .7

join elements.h -> helpers.rc


rmove -4 6

set lwidth .1 lstyle 2 just tc color gray40 hei 1 font plge arrowsize 0.4

box 4 2 justify tc round 0.1 name "scripts" fill gray10
      set color black lstyle 0 lwidth 0
      rmove 0 -.5
      text Scripts

set lwidth .08 color gray60 arrowsize .8

join scripts.rc -> elements.h
join scripts.tc -> core.v


rmove 12.5 8

box 4 7.5 just tc nobox fill gray5 

rmove 0 -.25

set color gray30 font pspr hei .8

text Legend

rmove -1 -1.5

begin box add .1 round 0.1 name "X" fill gray10
      text X
end box

rmove 2 0

begin box add .1 round 0.1 name "Y" fill gray10
      text Y
end box

set lwidth .05 arrowsize .4 hei .5 just tl
join X.rc -> Y.lc

rmove -2 -.75
write "interface of X\\depends on Y"


set hei .8 lwidth .08 just tc
rmove 0.35 -1.5

begin box add .1 round 0.1 name "X" fill gray10
      text X
end box

rmove 2 0

begin box add .1 round 0.1 name "Y" fill gray10
      text Y
end box

set lwidth .05 arrowsize .4 hei .5 just tl lstyle 2
join X.rc -> Y.lc

rmove -2 -.75
write "implementa-\\tion of X\\depends on Y"